plugins {
	id 'application'
	id 'checkstyle'
	id 'java-library'
	alias(libs.plugins.shadow)
}

dependencies {
	implementation project(':enigma')
	implementation project(':enigma-server')

	implementation libs.syntaxpain
	implementation libs.jopt
	implementation libs.flatlaf
	implementation libs.flatlaf.extras // for SVG icons
	implementation libs.quilt.config
	implementation libs.swing.dpi
	implementation libs.fontchooser
	testImplementation(testFixtures(project(':enigma')))
}

mainClassName = 'org.quiltmc.enigma.gui.Main'

jar.manifest.attributes 'Main-Class': mainClassName

static String convertToGradleTaskName(String name) {
	String newName = new String(name);

	for (int i = 0; i < name.length(); i++) {
		if (name.charAt(i) == '_') {
			var toReplace = '_' + name.charAt(i + 1)
			var newChar = name.charAt(i + 1).toUpperCase().toString()
			newName = newName.replace(toReplace, newChar)
		}
	}

	return newName
}

def registerTestTask(String name) {
	String taskName = convertToGradleTaskName(name);

	tasks.register("${taskName}TestGui", JavaExec.class) {
		group("test")
		dependsOn(":enigma:${taskName}TestObf")
		dependsOn(":enigma:processResources")
		mainClass = mainClassName
		classpath = sourceSets.test.runtimeClasspath

		def jar = project(":enigma").file("build/test-obf/${name}.jar")
		def profile = project(":enigma").file("build/resources/testFixtures/profile.json")
		def mappings = file("mappings/${name}")
		args('-jar', jar, '-mappings', mappings, '-profile', profile, '--development')
		doFirst {
			mappings.mkdirs()
		}
	}

	tasks.register("${taskName}TestGui2", JavaExec.class) {
		group("test")
		dependsOn(":enigma:${taskName}TestObf")
		mainClass = mainClassName
		classpath = sourceSets.test.runtimeClasspath

		def jar = project(":enigma").file("build/test-obf/${name}.jar")
		def profile = project(":enigma").file("build/resources/testFixtures/profile.json")
		def mappings = file("mappings/${name}2")
		args('-jar', jar, '-mappings', mappings, '-profile', profile, '--development')
		doFirst {
			mappings.mkdirs()
		}
	}
}

project(":enigma").file("src/test/java/org/quiltmc/enigma/input").listFiles().each {
	if (it.directory) {
		registerTestTask(it.name)
	}
}

registerTestTask("complete")

publishing {
	publications {
		"$project.name"(MavenPublication) {
			groupId project.group
			artifactId project.name
			version project.version
			from components.java
		}
	}
}
