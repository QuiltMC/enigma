import proguard.gradle.ProGuardTask

import java.nio.file.Files
import java.util.regex.Pattern

plugins {
	id 'checkstyle'
	id 'java-library'
	id 'java-test-fixtures'
}

// final PROVIDERS = project.providers

interface ExecProvider {
	@javax.inject.Inject
	ExecOperations getOperations()
}
final EXEC = objects.newInstance(ExecProvider).operations

sourceSets {
	dummyPlugin
}

configurations {
	dummyPluginRuntimeClasspath.extendsFrom runtimeClasspath
	dummyPluginCompileClasspath.extendsFrom compileClasspath
}

dependencies {
	implementation libs.bundles.asm

	implementation libs.vineflower
	implementation libs.cfr
	implementation libs.procyon
	implementation libs.javaparser

	implementation libs.quilt.config

	testImplementation libs.jimfs

	testFixturesImplementation libs.asm
	testFixturesImplementation libs.asm.tree

	dummyPluginImplementation(project)
}

// Generate "version.properties" file

final GEN_OUTPUT = project.layout.buildDirectory.dir('generated-resources')

tasks.register('generateVersionFile') {
	final outputFile = GEN_OUTPUT.map { it.file('version.properties') }

	inputs.property("version", project.version)
	inputs.property("vineflower-version", libs.vineflower.get().versionConstraint.displayName)
	inputs.property("cfr-version", libs.cfr.get().versionConstraint.displayName)
	inputs.property("procyon-version", libs.procyon.get().versionConstraint.displayName)

	outputs.file(outputFile)
	doLast {
		outputFile.get().asFile.text = inputs.properties.entrySet()
				.collect { "$it.key = $it.value" }.join("\n")
	}
}

sourceSets.main.output.dir GEN_OUTPUT, builtBy: generateVersionFile

static String taskNameFrom(String name) {
	final length = name.length()

	if (length == 0) {
		throw new IllegalArgumentException("name must not be empty!")
	}

	final builder = new StringBuilder()

	final first = name.charAt(0)

	final underscore = (char) '_'
	if (first != underscore) {
		builder.append(Character.toLowerCase(first))
	}

	for (int i = 1; i < length; i++) {
		final c = name.charAt(i)
		if (c == underscore) {
			if (i < length - 1) {
				builder.append(Character.toUpperCase(name.charAt(++i)))
			}
		} else {
			builder.append(Character.toLowerCase(c))
		}
	}

	return builder.toString()
}

// Generate obfuscated JARs for tests
// If your test fails for class file version problem with proguard, run gradle with -Dorg.gradle.java.home="<older jdk>" flag
def registerTestJarTasks(String name, String... input) {
	String taskName = taskNameFrom(name)
	final testJar = tasks.register("${taskName}TestJar", Jar.class) {
		group("test-setup")
		from(sourceSets.test.output) {
			include input
		}

		archiveFileName = "${name}.jar"
		destinationDirectory = file('build/test-inputs')
	}

	final customConfFile = file("src/test/resources/proguard-$name-test.conf")
	final confFileArg = customConfFile.exists() ? customConfFile
		: file('src/test/resources/proguard-test.conf')

	final testObf = tasks.register("${taskName}TestObf", ProGuardTask) {
		group("test-setup")

		configuration confFileArg

		libraryjars(
			[jarfilter: '!**.jar', filter: '!module-info.class'],
			"${System.getProperty('java.home')}/jmods/java.base.jmod"
		)

		injars testJar.map { it.archiveFile }

		outjars file("build/test-obf/${name}.jar")
	}

	test.dependsOn(testObf)
}

registerTestJarTasks("complete", "org/quiltmc/enigma/input/**/*.class")

file('src/test/java/org/quiltmc/enigma/input').listFiles().each { f ->
	if (f.directory) {
		registerTestJarTasks(f.name, "org/quiltmc/enigma/input/$f.name/**/*.class", "org/quiltmc/enigma/input/Keep.class")
	}
}

final dummyPluginJar = tasks.register('dummyPluginJar', Jar) {
	from sourceSets.dummyPlugin.output

	archiveFileName = "dummy-plugin.jar"
	destinationDirectory = project.layout.buildDirectory.dir('dummy-plugin')
}

// syncing, bumping, and jar-ing must all be one task because they all output to bumped-enigma-copy
final syncBumpAndJarEnigma = tasks.register("syncBumpAndJarEnigma", Sync) {
	inputs.property('projectVersion', project.version)

	final destPath = 'bumped-enigma-copy'

	from project.rootDir
	into project.layout.buildDirectory.map { it.dir(destPath) }

	final excludedProjectDirs = [ /*project(':enigma-cli'), */project(':enigma-server'), project(':enigma-swing') ]
		.collect { it.projectDir }

	final excludedPaths = [
		'.git', '.github', '.gradle', '.idea', 'gradlew', 'gradlew.bat', 'gradle/wrapper',
		'buildSrc/.gradle', 'buildSrc/build', 'enigma/src/dummyPlugin'
	].collect { rootProject.file(it) }

	final Set<File> excluded = excludedProjectDirs + excludedPaths

	exclude {
		it.file in excluded
	}

	// This can't be done lazily in doFirst because it needs to be excluded when task dependencies are checked
	// so the task doesn't depend on processDummyPluginResources.
	exclude {
		it.file in project.layout.buildDirectory.get().asFile
	}

	// already excluded whole build dir, but just in case, explicitly exclude dest dir to avoid copy recursion
	exclude {
		it.file in destinationDir
	}

	doFirst {
		// prevent accidental task recursion
		if (source.getElements().get().any { it.getAsFile().toPath().iterator().any { it.toString() == destPath } }) {
			throw new GradleException("${name} cannot be run from its own copy!")
		}
	}

	final projectRootDir = project.rootDir
	doLast {
		final dest = destinationDir.toPath()

		// only bump minor version, don't bother resetting patch to 0
		bumpCopyVersion(dest.resolve('build.gradle'), 'version', ~/(?m)^\s*version = '\d+.(\d+).\d+(?:\+.*)?'$/)
		bumpCopyVersion(
			dest.resolve('enigma/src/main/java/org/quiltmc/enigma/api/Enigma.java'),
			'MINOR_VERSION', ~/(?m)^\s*public static final int MINOR_VERSION = (\d+);$/
		)

		// this causes the task to always be out-of-date
		// copying gradle/wrapper, etc. and executing the copy instead did not help
		EXEC.exec {
			workingDir(destinationDir)

			final executableName = System.properties['os.name'].toLowerCase().contains('windows')
				? './gradlew.bat' : './gradlew'
			executable(projectRootDir.toPath().resolve(executableName))
			args ':enigma-cli:shadowJar'
		}
	}
}

static void bumpCopyVersion(java.nio.file.Path path, String variableName, Pattern pattern) {
	final matcher = pattern.matcher(path.text)
	if (matcher.find()) {
		final firstStart = matcher.start()

		final number = Integer.parseInt(matcher.group(1))

		final numberStart = matcher.start(1)
		final numberEnd = matcher.end(1)

		if (matcher.find()) {
			throw new GradleException(
				"Found multiple occurences of ${variableName} in ${path.last()} at ${firstStart} and ${matcher.start()}!"
			)
		}

		path.text = path.text.substring(0, numberStart) + (number + 1) + path.text.substring(numberEnd)
	} else {
		throw new GradleException("Failed to find ${variableName} in copy's ${path.last()}!")
	}
}

tasks.register('runDummyPluginAgainstBumpedEnigma', JavaExec) {
	final projectDir = project.projectDir
	final libsDir = project.layout.buildDirectory.dir('libs')
	final Provider<java.nio.file.Path> bumpedJarPath = syncBumpAndJarEnigma.map(Sync::getDestinationDir).zip(libsDir) { bumpDest, libs ->
		final relativeBuildLibs = projectDir.toPath().relativize(libs.asFile.toPath())
		// final copyBuildLibs = bumpDest.toPath().resolve('enigma').resolve(relativeBuildLibs)
		final copyBuildLibs = bumpDest.toPath().resolve('enigma-cli').resolve(relativeBuildLibs)

		return Files
			.find(copyBuildLibs, 1) { path, attributes -> path.fileName.toString().endsWith('-all.jar')}
			.sorted(Collections.reverseOrder())
			.findFirst()
			.orElseThrow { new GradleException('No bumped Enigma jar to run!') }
	}

	dependsOn(syncBumpAndJarEnigma, jar)

	final dummyPluginJarFile = dummyPluginJar.flatMap(Jar::getArchiveFile)

	inputs.files(bumpedJarPath, dummyPluginJarFile)

	mainClass = 'org.quiltmc.enigma.command.Main'

	classpath(bumpedJarPath, dummyPluginJarFile)

	doFirst {
		// DEBUG
		println "bumpedJar: ${bumpedJarPath.get()}"

		println('dummyJar: ' + dummyPluginJarFile.get().asFile)

		// classpath(bumpedJarPath.get(), dummyPluginJarFile.get().asFile)

		args 'print-stats', bumpedJarPath.get(), bumpedJarPath.get()

		// args('-jar', bumpedJarPath.get().toString())
		// classpath(dummyPluginJarFile.get().asFile.toString())
	}
}

publishing {
	publications {
		"$project.name"(MavenPublication) {
			groupId project.group
			artifactId project.name
			version project.version
			from components.java
		}
	}
}
