repositories {
    // Yes jcenter is slated for extermination, we just need to wait for proguard to move over
    jcenter()
}

configurations {
    proGuard
}

dependencies {
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-commons:9.2'
    implementation 'org.ow2.asm:asm-tree:9.2'
    implementation 'org.ow2.asm:asm-util:9.2'

    implementation 'org.quiltmc:procyon-quilt-compilertools:0.5.35.local' // TODO: Fix the buildscript
    implementation 'org.quiltmc:cfr:0.0.2'

    // TODO: When will proguard publish to something other than jcenter
    proGuard 'com.guardsquare:proguard-base:7.1.0-beta5'
}

// Generate "version.txt" file

ext.genOutputDir = file("$buildDir/generated-resources")

task generateVersionFile {
    ext.outputFile = file("$genOutputDir/version.txt")
    outputs.file(outputFile)
    doLast {
        outputFile.text = "${project.version}"
    }
}

sourceSets.main.output.dir genOutputDir, builtBy: generateVersionFile

// Generate obfuscated JARs for tests

def libraryJarsArg = JavaVersion.current().java9Compatible ? "<java.home>/jmods" : "<java.home>/lib/rt.jar"

// If your test fails for class file version problem with proguard, run gradle with -Dorg.gradle.java.home="<older jdk>" flag
file('src/test/java/cuchaz/enigma/inputs').listFiles().each { theFile ->
    if (theFile.directory) {
        task("${theFile.name}TestJar", type: Jar) {
            from(sourceSets.test.output) {
                include "cuchaz/enigma/inputs/$theFile.name/**/*.class"
                include 'cuchaz/enigma/inputs/Keep.class'
            }

            archiveFileName = theFile.name + '.jar'
            destinationDirectory = file('build/test-inputs')
        }

        task("${theFile.name}TestObf", type: JavaExec,
                dependsOn: "${theFile.name}TestJar") {
            main 'proguard.ProGuard'
            classpath configurations.proGuard

            args '@src/test/resources/proguard-test.conf', '-injars', file('build/test-inputs/' +
                    "${theFile.name}.jar"), '-libraryjars', libraryJarsArg,
                    '-outjars', file('build/test-obf/' + "${theFile.name}.jar")
        }

        test.dependsOn "${theFile.name}TestObf"
    }
}

test.dependsOn 'translationTestObf'

publishing {
    publications {
        "$project.name"(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
        }
    }
}
